plugins {
    id("net.ltgt.errorprone") version "0.6"
}


apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.philjay'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 3
        versionName '3.1.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true // this prevents "not mocked" error
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'androidx.annotation:annotation:1.0.0'
    testImplementation 'junit:junit:4.12'


    annotationProcessor "com.uber.nullaway:nullaway:0.10.8"
    annotationProcessor "edu.ucr.cs.riple.annotator:annotator-scanner:1.3.6-alpha-5"

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    errorprone "com.google.errorprone:error_prone_core:2.3.2"
    errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"
    compileOnly "com.uber.nullaway:nullaway-annotations:0.10.10"
    compileOnly "org.jspecify:jspecify:0.3.0"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

import net.ltgt.gradle.errorprone.CheckSeverity

def scanner_path =  "${rootDir.getAbsolutePath()}" +"/out/scanner.xml"
def nullaway_path =  "${rootDir.absolutePath}/out/checker.xml"
def annotator_mode = (System.getenv("ANNOTATOR_MODE") != null)
def nullaway_off = (System.getenv("NULLAWAY_OFF") != null)

tasks.withType(JavaCompile) {
    // remove the if condition if you want to run NullAway on test code
    if (!name.toLowerCase().contains("test")) {
        options.errorprone.disableAllChecks = true
//        options.errorprone.disableAllWarnings = true
        options.errorprone {
            check("NullAway", nullaway_off ? CheckSeverity.OFF : CheckSeverity.ERROR)
            check("AnnotatorScanner", annotator_mode ? CheckSeverity.ERROR : CheckSeverity.OFF)
            option("NullAway:AnnotatedPackages", "com.github.mikephil.charting")
            option("NullAway:SerializeFixMetadata", annotator_mode ? "true" : "false")
            option("NullAway:FixSerializationConfigPath", nullaway_path)
            option("AnnotatorScanner:ConfigPath", scanner_path)
        }
    }
    options.compilerArgs << "-Xmaxerrs" << "100000"
    options.compilerArgs << "-Xmaxwarns" << "100000"
}

artifacts {
    archives sourcesJar
}
